target C {
    platform: "rp2040",
    threading: true,
    workers: 2,
    tracing: false,
    scheduler: "STATIC",
    //logging: "debug",
    build-type: "Debug",
}

preamble {=
    #include <pico/stdlib.h>
    #include <hardware/gpio.h>
=}
/**
* Multicore test that attempts to concurrently
* print from both cores using parallel reactors
*
* TODO: read about peripheral access from both cores
* the possibility of using a shared memory space
* 
* @author Abhi Gundrala
**/

reactor GroupOne {
    timer t0(0, 200 msec);
    state g_led:bool;
    
    reaction(startup) {=
        // green_led
        gpio_init(19);
        gpio_set_dir(19, GPIO_OUT);
    =}
    
    reaction(t0) {=
        // toggle led
        self->g_led = !self->g_led; 
        gpio_put(19, self->g_led);
        // print core number
        printf("core %d\n", 
            get_core_num());
    =}
}

reactor GroupTwo {
    timer t1(0, 300 msec);
    state r_led:bool;
    state y_led:bool;
    
    reaction(startup) {=
        // red_led
        gpio_init(21);
        gpio_set_dir(21, GPIO_OUT);
        // yellow_led
        gpio_init(20);
        gpio_set_dir(20, GPIO_OUT);
    =}
    
    reaction(t1) {=
        // toggle leds
        self->r_led = !self->r_led; 
        gpio_put(21, self->r_led);
        self->y_led = !self->y_led; 
        gpio_put(20, self->y_led);
        // print core number
        printf("core %d\n", 
            get_core_num());
    =}
}

main reactor {
    g1 = new GroupOne();
    g2 = new GroupTwo();
}
