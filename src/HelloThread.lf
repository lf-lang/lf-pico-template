target C {
    platform: "rp2040",
    threading: true,
    workers: 2,
    tracing: false,
    //scheduler: STATIC,
    //logging: "debug",
    build-type: "Debug",
}

/**
* Multicore test that attempts to concurrently
* print from both cores using parallel reactors
* 
* @author Abhi Gundrala
**/

import Led from "./lib/Led.lf";

reactor GroupOne {
    timer t0(0, 200 msec);
    
    green_led = new Led(pin=19);
    
    reaction(t0) -> green_led.tog {=
        // toggle led
        lf_set(green_led.tog, true); 
        // print core number
        printf("core %d\n", 
            get_core_num());
    =}
}

reactor GroupTwo {
    timer t1(0, 300 msec);
    
    red_led = new Led(pin=21);
    yellow_led = new Led(pin=20);
    
    reaction(t1) -> yellow_led.tog, red_led.tog {=
        // toggle leds
        lf_set(red_led.tog, true); 
        lf_set(yellow_led.tog, true); 
        // print core number
        printf("core %d\n", 
            get_core_num());
    =}
}

main reactor {
    g1 = new GroupOne();
    g2 = new GroupTwo();
}
